task patchedJar(type: Copy)

configurations {
	asm
}

dependencies {
	asm group: "org.ow2.asm", name: "asm", transitive: false, version: "7.0"
	asm group: "org.ow2.asm", name: "asm-commons", transitive: false, version: "7.0"

	compileOnly group: "org.apache.aries.spifly", name: "org.apache.aries.spifly.dynamic.framework.extension", version: "1.0.14"
}

jar {
	deleteAllActions()

	dependsOn patchedJar
}

patchedJar {
	File tmpDir = new File (buildDir, "tmp/patchedJar")

	duplicatesStrategy = "exclude"
	exclude "module-info.class"
	into tmpDir

	from {
		sourceSets.main.output
	}

	from {
		configurations.asm.collect {
			zipTree(it).matching {
				include "**/*.class"
			}
		}
	}

	from {
		zipTree(configurations.compileOnly.singleFile).matching {
			exclude "org/objectweb/asm/**"
		}
	}

	doFirst {
		delete tmpDir
	}

	doLast {
		ant.jar(destFile: jar.archivePath, manifest: new File(tmpDir, "META-INF/MANIFEST.MF")) {
			zipFileSet(dir: tmpDir)
		}
	}
}

updateFileVersions {
	replaceOnlyIf([{
		String group, String replacement, String content, File contentFile ->

		if (buildFile == contentFile) {
			return false
		}

		return true
	}])
}